buildscript {
	ext {
		springBootVersion = '1.5.14.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.poc.event.sourcing.bank'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	cucumberRuntime {
		extendsFrom testRuntime
	}
}

task cucumber() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "cucumber.api.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = ['--plugin', 'pretty', '--glue', 'com.poc.event.sourcing.bank.cucumber', 'src/test/resources/features']
		}
	}
}

cucumber.mustRunAfter test

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile ('org.apache.httpcomponents:httpclient:4.5.6')
	compile('de.codecentric:spring-boot-admin-starter-client:1.5.4')
	compile('org.axonframework:axon-spring-boot-starter:3.3.2')
	compile('org.axonframework:axon-mongo:3.3.2')
	compile('org.modelmapper:modelmapper:0.7.7')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('io.springfox:springfox-swagger2:2.7.0')
	compile('io.springfox:springfox-swagger-ui:2.7.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile 'io.cucumber:cucumber-java8:2.3.1'
	testCompile group: 'info.cukes', name: 'gherkin', version: '2.12.2'
	testCompile 'io.cucumber:cucumber-spring:2.3.1'
	testCompile 'io.cucumber:cucumber-junit:2.3.1'
}
